(ns advent-2019.day-10
  (:require [clojure.string :as str]
            [clojure.set :as set]))

(defn parse [s]
  (apply mapv
         vector
         (str/split-lines s)))

(defn line
  "The constants for the equation y=mx+b

  Undefined slope returns nil."
  [[x1 y1] [x2 y2]]
  (when-not (= x1 x2)
    (let [m (/ (- y2 y1)
               (- x2 x1))]
      {:m m
       :b (- y1 (* m x1))})))

(defn solve-y [{m :m
                b :b} x]
  (+ (* m x)
     b))

(defn points-between [pnt1 pnt2]
  (let [l (line pnt1 pnt2)
        [[x1 y1] [x2 y2]] (sort [pnt1 pnt2])]
    (if l
      (into #{}
            (keep (fn [x]
                    (let [y (solve-y l x)]
                      (when (= y (long y))
                        [x y]))))
            (range (inc x1) x2))
      (into #{}
            (map (fn [y]
                   [x1 y]))
            (range (inc y1) y2)))))

(comment
  (points-between [0 0]
                  [0 5]))

(defn asteroids [board]
  (into #{}
        (comp (map-indexed (fn [i col]
                             (keep-indexed (fn [j c]
                                             (when (= c \#)
                                               [i j]))
                                           col)))
              cat)
        board))

(defn part-1 [in]
  (let [b (parse in)
        ast (asteroids b)
        blocked? (fn [p1 p2]
                   (boolean (seq (set/intersection (points-between p1 p2)
                                                   ast))))]
    (apply max-key
           second
           (for [a ast]
             [a (count (remove (fn [a']
                                 (blocked? a a'))
                               (disj ast a)))]))))

(defn angle [[x1 y1] [x2 y2]]
  (let [theta (- (Math/atan2 (- x1 x2)
                             (- y1 y2)))]
    (* (if (neg? theta)
         (+ (* 2 Math/PI)
            theta)
         theta)
       (/ 180
          Math/PI))))

(defn station [ast]
  (let [blocked? (fn [p1 p2]
                   (boolean (seq (set/intersection (points-between p1 p2)
                                                   ast))))]
    (apply max-key
           (fn [a]
             (count (remove (fn [a']
                              (blocked? a a'))
                            (disj ast a))))
           ast)))

(defn vaporize-order [ast]
  (let [s (station ast)]
    (sort-by (fn [p]
               (+ (* 360
                     (count (set/intersection (points-between s p)
                                              ast)))
                  (angle s p)))
             (disj ast s))))

(defn part-2 [in]
  (let [b (parse in)
        ast (asteroids b)
        [x y] (nth (vaporize-order ast)
                   199)]
    (+ (* x 100)
       y)))



(comment
  (part-1 ".#..#\n.....\n#####\n....#\n...##")
  (part-1 "......#.#.\n#..#.#....\n..#######.\n.#.#.###..\n.#..#.....\n..#....#.#\n#..#....#.\n.##.#..###\n##...#..#.\n.#....####")
  (part-1 "#.#...#.#.\n.###....#.\n.#....#...\n##.#.#.#.#\n....#.#.#.\n.##..###.#\n..#...##..\n..##....##\n......#...\n.####.###.")
  (part-1 ".#..#..###\n####.###.#\n....###.#.\n..###.##.#\n##.##.#.#.\n....###..#\n..#.#..#.#\n#..#.#.###\n.##...##.#\n.....#.#..")
  (vaporize-order (asteroids (parse ".#....#####...#..\n##...##.#####..##\n##...#...#.#####.\n..#.....#...###..\n..#.#.....#....##")))
  (part-2 ".#..##.###...#######\n##.############..##.\n.#.######.########.#\n.###.#######.####.#.\n#####.##.#.##.###.##\n..#####..#.#########\n####################\n#.####....###.#.#.##\n##.#################\n#####.##.###..####..\n..######..##.#######\n####.##.####...##..#\n.#####..#.######.###\n##...#.##########...\n#.##########.#######\n.####.#.###.###.#.##\n....##.##.###..#####\n.#.#.###########.###\n#.#.#.#####.####.###\n###.##.####.##.#..##")
  (part-2 "#.#................#..............#......#......\n.......##..#..#....#.#.....##...#.........#.#...\n.#...............#....#.##......................\n......#..####.........#....#.......#..#.....#...\n.....#............#......#................#.#...\n....##...#.#.#.#.............#..#.#.......#.....\n..#.#.........#....#..#.#.........####..........\n....#...#.#...####..#..#..#.....#...............\n.............#......#..........#...........#....\n......#.#.........#...............#.............\n..#......#..#.....##...##.....#....#.#......#...\n...#.......##.........#.#..#......#........#.#..\n#.............#..........#....#.#.....#.........\n#......#.#................#.......#..#.#........\n#..#.#.....#.....###..#.................#..#....\n...............................#..........#.....\n###.#.....#.....#.............#.......#....#....\n.#.....#.........#.....#....#...................\n........#....................#..#...............\n.....#...#.##......#............#......#.....#..\n..#..#..............#..#..#.##........#.........\n..#.#...#.......#....##...#........#...#.#....#.\n.....#.#..####...........#.##....#....#......#..\n.....#..#..##...............................#...\n.#....#..#......#.#............#........##...#..\n.......#.....................#..#....#.....#....\n#......#..###...........#.#....#......#.........\n..............#..#.#...#.......#..#.#...#......#\n.......#...........#.....#...#.............#.#..\n..##..##.............#........#........#........\n......#.............##..#.........#...#.#.#.....\n#........#.........#...#.....#................#.\n...#.#...........#.....#.........#......##......\n..#..#...........#..........#...................\n.........#..#.......................#.#.........\n......#.#.#.....#...........#...............#...\n......#.##...........#....#............#........\n#...........##.#.#........##...........##.......\n......#....#..#.......#.....#.#.......#.##......\n.#....#......#..............#.......#...........\n......##.#..........#..................#........\n......##.##...#..#........#............#........\n..#.....#.................###...#.....###.#..#..\n....##...............#....#..................#..\n.....#................#.#.#.......#..........#..\n#........................#.##..........#....##..\n.#.........#.#.#...#...#....#........#..#.......\n...#..#.#......................#...............#"))
